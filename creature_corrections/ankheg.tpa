// Ankheg fixes

COPY_EXISTING "b1-8.itm" "override/mh#ankhg"
  SAY NAME1 "Attack"
  SAY NAME2 "Attack"

  WRITE_ASCII 0x003a "iwolf"

  LAUNCH_PATCH_FUNCTION ALTER_ITEM_HEADER
    INT_VAR
    dicenumber		= 3
    STR_VAR
    icon		= "iwolf"
  END

  LAUNCH_PATCH_FUNCTION ADD_ITEM_EFFECT
    INT_VAR
    opcode		= 12	// HP: Damage
    target		= 2	// Pre-target
    timing		= 1	// Permanent
    parameter2		= 65536
    dicenumber		= 1
    dicesize		= 4
  END


ADD_SPELL "%MOD_FOLDER%/creature_corrections/ankheg_acid_squirt.spl" 3 1 MH_ANKHEG_ACID_SQUIRT
  SAY NAME1 "Ankheg Acid Squirt"


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/creature_corrections/mh#ankhg.baf"
END


DEFINE_PATCH_MACRO fix_ankheg
BEGIN
  REPLACE_CRE_ITEM "mh#ankhg" #0 #0 #0 "unstealable&undroppable" "weapon1" EQUIP
  REPLACE_CRE_ITEM "mh#ankhg" #0 #0 #0 "unstealable&undroppable" "weapon2"
  REPLACE_CRE_ITEM "mh#ankhg" #0 #0 #0 "unstealable&undroppable" "weapon3"
  REPLACE_CRE_ITEM "mh#ankhg" #0 #0 #0 "unstealable&undroppable" "weapon4"

  WRITE_BYTE	0x0052 13	// THAC0
  WRITE_BYTE	0x0053 1	// # of attacks
  WRITE_BYTE	0x0234 8	// Level (HD)
  WRITE_BYTE	0x0238 18	// Strength
  WRITE_BYTE	0x0239 50	// Strength extra


  LAUNCH_PATCH_FUNCTION replace_cre_script
    INT_VAR
    check_default	= 0
    STR_VAR
    old_script		= "ankheg"
    new_script		= "mh#ankhg"
    RET
    found
  END

  PATCH_IF !found
  BEGIN
    WRITE_ASCII	SCRIPT_RACE	"mh#ankhg"
  END

  WRITE_ASCII	SCRIPT_DEFAULT	"wtasight"
END


