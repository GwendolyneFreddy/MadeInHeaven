// Dread Wolf corrections

COPY_EXISTING "p1-10.itm" "override/mh#wlfdr.itm"
  SAY NAME1 "Attack"
  SAY NAME2 "Attack"

  WRITE_ASCII 0x003a "iwolf"

  LAUNCH_PATCH_FUNCTION ALTER_ITEM_HEADER
    STR_VAR
    icon		= "iwolf"
  END

  LAUNCH_PATCH_FUNCTION ADD_ITEM_EQEFFECT
    INT_VAR
    opcode		= 98	// HP: Regenerate
    target		= 1	// Self
    timing		= 2	// While equipped
    parameter1		= 2
    parameter2		= 3	// Regen 1 HP every <parameter1> seconds
  END

  LAUNCH_PATCH_FUNCTION ADD_ITEM_EFFECT
    INT_VAR
    opcode		= 78	// State: Disease
    target		= 2	// Pre-target
    duration		= 300	// 5 turns
    parameter1		= 60
    parameter2		= 3	// Lose 1 HP every <parameter1> seconds
    savingthrow		= 4	// Death/Poison
  END

  LAUNCH_PATCH_FUNCTION ADD_ITEM_EFFECT
    INT_VAR
    opcode		= 142	// Graphics: Display Special Effect Icon
    target		= 2	// Pre-target
    duration		= 300	// 5 turns
    parameter2		= 7	// Diseased
    savingthrow		= 4	// Death/Poison
  END


DEFINE_PATCH_MACRO fix_dread_wolf
BEGIN
  REPLACE_CRE_ITEM "mh#wlfdr" #0 #0 #0 "unstealable&undroppable" "weapon1" EQUIP
  REPLACE_CRE_ITEM "mh#wlfdr" #0 #0 #0 "unstealable&undroppable" "weapon2"
  REPLACE_CRE_ITEM "mh#wlfdr" #0 #0 #0 "unstealable&undroppable" "weapon3"
  REPLACE_CRE_ITEM "mh#wlfdr" #0 #0 #0 "unstealable&undroppable" "weapon4"
  ADD_CRE_ITEM "ring95" #0 #0 #0 "unstealable&undroppable" "ring"

  WRITE_BYTE	0x0052 15	// THAC0
  WRITE_BYTE	0x0271 IDS_OF_SYMBOL("general" "undead")
END


