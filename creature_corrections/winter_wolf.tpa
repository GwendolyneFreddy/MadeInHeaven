// Winter Wolf fixes

COPY_EXISTING "p2-8.itm" "override/wolfwi1.itm"
  SAY NAME1 "Attack"
  SAY NAME2 "Attack"

  WRITE_ASCII 0x003a "iwolf"

  LAUNCH_PATCH_FUNCTION ALTER_ITEM_HEADER
    STR_VAR
    icon		= "iwolf"
  END

  LAUNCH_PATCH_FUNCTION ADD_ITEM_EFFECT
    INT_VAR
    opcode		= 12	// HP: Damage
    target		= 2	// Pre-target
    timing		= 1	// Permanent
    parameter2		= 131072
    probability1	= 40
    dicenumber		= 1
    dicesize		= 6
  END


ADD_SPELL "%MOD_FOLDER%/creature_corrections/winter_wolf_breath.spl" 3 1 MH_WINTER_WOLF_BREATH
  SAY NAME1 "Winter Wolf Breath"


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/creature_corrections/wntrwolf.baf"
END


DEFINE_PATCH_MACRO fix_winter_wolf
BEGIN
  REMOVE_CRE_ITEM "%tutu_var%wolfwi2"
  REPLACE_CRE_ITEM "wolfwi1" #0 #0 #0 "unstealable&undroppable" "weapon1" EQUIP

  WRITE_BYTE	0x0052 15	// THAC0

  WRITE_ASCII	SCRIPT_RACE	"wntrwolf"
  WRITE_ASCII	SCRIPT_DEFAULT	"wtasight"
END


