// MiH Item Pack for BG1

LAUNCH_ACTION_FUNCTION "install_component"
  STR_VAR
  component_name	= "mih_item_pack"
END


// =============
// Brokk's Forge
// =============

COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#brok1.cre" "override"
  SAY NAME1 "Brokk"
  SAY NAME2 "Brokk"

  PATCH_IF ENGINE_IS "bg1 totsc"
  BEGIN
    WRITE_BYTE 0x0072 3
    WRITE_BYTE 0x0071 1
    WRITE_BYTE 0x0074 3
  END
  ELSE
  BEGIN
    SET_BG2_PROFICIENCY PROFICIENCYAXE 3
    SET_BG2_PROFICIENCY PROFICIENCYWARHAMMER 3
    SET_BG2_PROFICIENCY PROFICIENCYSWORDANDSHIELD 1
  END

LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/item_pack/bgt/dialog/mh#brok1.d"
END

COPY "%MOD_FOLDER%/item_pack/bgt/stores/mh#brok1.sto" "override"
  SAY NAME2 "Brokk's Forge"


COPY_EXISTING "%FriendlyArmInn_House%.are" "override"
  LAUNCH_PATCH_MACRO remove_all_area_actors

  LAUNCH_PATCH_FUNCTION add_area_actor
    INT_VAR
    x_position		= 450
    y_position		= 300
    orientation		= 2
    STR_VAR
    cre_resref		= "mh#brok1"
  END


// ============================
// Tania the magical seamstress
// ============================

COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#tania.cre" "override"
  SAY NAME1 "Tania"
  SAY NAME2 "Tania"

  PATCH_IF ENGINE_IS "bg1 totsc"
  BEGIN
    WRITE_BYTE 0x006f 1
    WRITE_BYTE 0x0072 1
  END
  ELSE
  BEGIN
    SET_BG2_PROFICIENCY PROFICIENCYQUARTERSTAFF 1
    SET_BG2_PROFICIENCY PROFICIENCYDAGGER 1
  END


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/item_pack/bgt/dialog/mh#tania.d"
END

COPY_EXISTING "%NashkelCarnival_ArmorTent%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actor
    INT_VAR
    x_position		= 660
    y_position		= 330
    orientation		= 2
    STR_VAR
    cre_resref		= "mh#tania"
  END


// =======================
// Easter Egg Replacements
// =======================

COPY_EXISTING "%FriendlyArmInn%.are" "override"
  LAUNCH_PATCH_FUNCTION REPLACE_AREA_ITEM
    STR_VAR
    old_item		= "%tutu_scriptbg%ring08"
    new_item		= "mh#ring2"
  END


COPY_EXISTING "%CoastWay%.are" "override"
  LAUNCH_PATCH_FUNCTION REPLACE_AREA_ITEM
    STR_VAR
    old_item		= "%tutu_var%ring06"
    new_item		= "mh#ring3"
  END


COPY_EXISTING "%GibberlingMountains%.are" "override"
  LAUNCH_PATCH_FUNCTION REPLACE_AREA_ITEM
    STR_VAR
    old_item		= "%tutu_var%ring02"
    new_item		= "mh#ring1"
  END


COPY_EXISTING "%Nashkel%.are" "override"
  LAUNCH_PATCH_FUNCTION REPLACE_AREA_ITEM
    STR_VAR
    old_item		= "%tutu_var%plat06"
    new_item		= "mh#ioun1"
  END


// ==============================
// Varscona & Heartsblood shuffle
// ==============================

COPY_EXISTING "%tutu_var%greywo.cre" "override"
  REPLACE_CRE_ITEM "mh#swrd2" #0 #0 #0 "none" "weapon1" EQUIP


COPY_EXISTING "%tutu_var%raemon.cre" "override"
  REPLACE_CRE_ITEM "%tutu_var%sw1h06" #0 #0 #0 "none" "weapon1" EQUIP


// =====================
// Mutamin and his cloak
// =====================

COPY_EXISTING "%tutu_var%mutami.cre" "override"
  ADD_CRE_ITEM "mh#clck1" #0 #0 #0 "none" "cloak"


// =======================
// New items for Thalantyr
// =======================

COPY_EXISTING "%tutu_scripth%ighhedg.sto" "override"
  ADD_STORE_ITEM "mh#potn1" AFTER "%tutu_var%potn38" #1 #0 #0 "identified" #3
  ADD_STORE_ITEM "mh#potn2" AFTER "%tutu_var%potn45" #1 #0 #0 "identified" #1
  ADD_STORE_ITEM "mh#amul2" LAST #1 #0 #0 "identified" #1


// ======================================
// New item in Song of the Morning temple
// ======================================

COPY_EXISTING "%tutu_var%tem3402.sto" "override"
  ADD_STORE_ITEM "mh#amul1" LAST #0 #0 #0 "identified" #1


// =============================
// New item in Temple of Yondala
// =============================

COPY_EXISTING "%tutu_var%tem4003.sto" "override"
  ADD_STORE_ITEM "mh#amul4" LAST #0 #0 #0 "identified" #1


// =========================================
// New items in Carnival magic trinkets shop
// =========================================

COPY_EXISTING "%tutu_var%sto4906.sto" "override"
  ADD_STORE_ITEM "mh#misc4" LAST #1 #0 #0 "identified" #1
  ADD_STORE_ITEM "mh#boot2" LAST #0 #0 #0 "identified" #1


// ====================
// Bjornin's new shield
// ====================

COPY_EXISTING "%tutu_var%bjorni.cre" "override"
  REPLACE_CRE_ITEM "mh#shld1" #0 #0 #0 "none" "qitem1"


COPY_EXISTING "%tutu_var%bjorni.dlg" "override"
  DECOMPILE_AND_PATCH
  BEGIN
    REPLACE_TEXTUALLY "%tutu_var%shld04" "mh#shld1"
  END


// =============================
// New potions in various places
// =============================

COPY_EXISTING "%tutu_var%sto1115.sto" "override"
	      "%tutu_var%sto4905.sto" "override"
  ADD_STORE_ITEM "mh#potn1" AFTER "%tutu_var%potn36" #1 #0 #0 "identified" #2
  ADD_STORE_ITEM "mh#potn2" AFTER "%tutu_var%potn46" #1 #0 #0 "identified" #2


COPY_EXISTING "%FishingVillage_AnkhegPit%.are" "override"
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 1
    charges		= 1
    STR_VAR
    item_to_add		= "mh#potn1"
  END


COPY_EXISTING "%UlcasterRuins%.are" "override"
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 2
    charges		= 1
    STR_VAR
    item_to_add		= "mh#potn1"
  END


// ===========================
// New item in The Lady's Hall
// ===========================

COPY_EXISTING "%tutu_var%tem0132.sto" "override"
  ADD_STORE_ITEM "mh#amul3" LAST #3 #1 #1 "identified" #1


// ========================
// The Great Hammer Shuffle
// ========================

COPY_EXISTING "%tutu_var%hurgan.cre" "override"
  REPLACE_CRE_ITEM "mh#hamm1" #0 #0 #0 "none" "qitem3"


COPY_EXISTING "%tutu_var%hurgan.dlg" "override"
  DECOMPILE_AND_PATCH
  BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "%tutu_var%hamm04" "mh#hamm1"
  END


COPY_EXISTING "%tutu_var%bassil.cre" "override"
  REPLACE_CRE_ITEM "%tutu_var%hamm04" #0 #0 #0 "none" "weapon1" EQUIP


COPY_EXISTING "%tutu_var%taugos.cre" "override"
  REPLACE_CRE_ITEM "%tutu_var%hamm03" #0 #0 #0 "none" "weapon1" EQUIP


// ===============================
// New item in Black Alaric's Cave
// ===============================

COPY_EXISTING "%Lighthouse_BlackAlaricsCave%.are" "override"
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 1
    STR_VAR
    item_to_add		= "mh#swrd3"
  END


// =======================
// New Cloakwood encounter
// =======================

COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#drikx.cre" "override"
  SAY NAME1 "Drikx"
  SAY NAME2 "Drikx"

  PATCH_IF ENGINE_IS "bg1 totsc"
  BEGIN
    WRITE_BYTE	0x006e	5	// Large swords proficiency
    WRITE_BYTE	0x004c	"-4"	// Simulate sword&shield prof.
  END
  ELSE
  BEGIN
    SET_BG2_PROFICIENCY	PROFICIENCYBASTARDSWORD 5
    SET_BG2_PROFICIENCY	PROFICIENCYSWORDANDSHIELD 2
  END


COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#strok.cre" "override"
  SAY NAME1 "Strok"
  SAY NAME2 "Strok"

  PATCH_IF ENGINE_IS "bg1 totsc"
  BEGIN
    WRITE_BYTE	0x006e	2	// Large sword proficiency
    WRITE_BYTE	0x0070	5	// Bow proficiency
  END
  ELSE
  BEGIN
    SET_BG2_PROFICIENCY	PROFICIENCYLONGBOW 5
    SET_BG2_PROFICIENCY	PROFICIENCYLONGSWORD 2
  END


COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#gnung.cre" "override"
  SAY NAME1 "Gnung"
  SAY NAME2 "Gnung"

  PATCH_IF ENGINE_IS "bg1 totsc"
  BEGIN
    WRITE_BYTE	0x006e	2	// Large sword proficiency
    WRITE_BYTE	0x0070	5	// Bow proficiency
  END
  ELSE
  BEGIN
    SET_BG2_PROFICIENCY	PROFICIENCYLONGBOW 5
    SET_BG2_PROFICIENCY	PROFICIENCYLONGSWORD 2
  END


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/item_pack/bgt/dialog/cloakwood_assassins.d"
END


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/item_pack/bgt/scripts/mh#cwass.baf"
END


COPY_EXISTING "%CloakwoodWyverns%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/item_pack/bgt/tables/bg1700.2da"
  END


// ===============
// Cloakwood Mines
// ===============

COPY_EXISTING "%CloakwoodMines_L3%.are" "override"
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 29
    STR_VAR
    item_to_add		= "mh#btax1"
  END

  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 39
    STR_VAR
    item_to_add		= "mh#swrd4"
  END


// ===============================
// New items in Sorcerous Sundries
// ===============================

COPY_EXISTING "%tutu_var%sto0703.sto" "override"
  ADD_STORE_ITEM "mh#potn1" AFTER "%tutu_var%potn39" #1 #0 #0 "identified" #3
  ADD_STORE_ITEM "mh#potn2" AFTER "%tutu_var%potn46" #1 #0 #0 "identified" #1
  ADD_STORE_ITEM "mh#boot3" LAST #1 #1 #0 "identified" #1
  ADD_STORE_ITEM "mh#clck3" LAST #2 #0 #0 "identified" #1
  ADD_STORE_ITEM "mh#ring5" LAST #0 #0 #0 "identified" #1


// ========================
// New items for Black Lily
// ========================

COPY_EXISTING "%tutu_scripts%toblack.sto" "override"
  ADD_STORE_ITEM "mh#boot1" LAST #0 #0 #0 "identified" #1


// ====================
// Candlekeep Catacombs
// ====================

COPY_EXISTING "%Candlekeep_Catacombs_L1%.are" "override"
  // Add some Oils of Acid Resistance
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 1
    charges		= 2
    STR_VAR
    item_to_add		= "mh#potn1"
  END

  // Put Ring of Protection +3 in previously empty grave
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 4
    STR_VAR
    item_to_add		= "mh#ring4"
  END


// ================
// Isle of Balduran
// ================

// Letha's quest

COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#helen.cre" "override"
  SAY NAME1 "Helen"
  SAY NAME2 "Helen"


COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#letha.cre" "override"
  SAY NAME1 "Letha"
  SAY NAME2 "Letha"


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/item_pack/bgt/scripts/mh#helen.baf"
END


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/item_pack/bgt/dialog/letha_helen.d"
END

COPY_EXISTING "%IsleofBalduranS%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actor
    INT_VAR 
    x_position		= 345
    y_position		= 3640
    orientation		= 3
    STR_VAR
    cre_resref		= "mh#helen"
  END


COPY_EXISTING "%IsleofBalduranN%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actor
    INT_VAR 
    x_position		= 225
    y_position		= 2615
    orientation		= 1
    STR_VAR
    cre_resref		= "mh#letha"
  END


// New items in shipwreck

COPY_EXISTING "%IsleofBalduranNShip_D1%.are" "override"
  // Add Meteoric Iron
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 1
    STR_VAR
    item_to_add		= "mh#misc2"
  END


COPY_EXISTING "%IsleofBalduranNShip_D2%.are" "override"
  // Add Spear of Halama
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 3
    STR_VAR
    item_to_add		= "mh#sper1"
  END


// ==============
// Durlag's Tower
// ==============

// New item on Kirinhale

COPY_EXISTING "%tutu_var%kirinh.cre" "override"
  ADD_CRE_ITEM "mh#belt3" #0 #0 #0 "none" "belt"


// New items and encounter on D1

COPY_EXISTING "%tutu_scriptbg%skelwa.cre" "override/mh#skwft.cre"
  REPLACE_CRE_ITEM "%tutu_var%sw1h24" #0 #0 #0 "none" "weapon1" EQUIP
  ADD_CRE_ITEM "%tutu_var%shld06" #0 #0 #0 "none" "shield"
  REPLACE_CRE_ITEM "%tutu_var%helm12" #0 #0 #0 "none" "helmet"


COPY_EXISTING "%DurlagsTower_D1%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    tutu_compat		= 1
    bgt_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/item_pack/bgt/tables/bg0511.2da"
  END

  // Add Gauntlets of the Dwarven King
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 19
    STR_VAR
    item_to_add		= "mh#brac2"
  END


// New items on D4

COPY_EXISTING "%DurlagsTower_D4%.are" "override"
  // Replace Cloak of the Shield with Battlecloak of Vycaena
  LAUNCH_PATCH_FUNCTION REPLACE_AREA_ITEM
    STR_VAR
    old_item		= "%tutu_var%clck20"
    new_item		= "mh#clck2"
  END

  // Replace Krogan's Skullcrusher with Blessed Mace of Ordona
  LAUNCH_PATCH_FUNCTION REPLACE_AREA_ITEM
    STR_VAR
    old_item		= "%tutu_var%blun11"
    new_item		= "mh#blun2"
  END

  // Replace Bracers of Defense AC 8 with Bracers of Winter's Chill
  LAUNCH_PATCH_FUNCTION REPLACE_AREA_ITEM
    STR_VAR
    old_item		= "%tutu_var%brac01"
    new_item		= "mh#brac1"
  END

  // Replace Flame Tongue with Catclaw +3
  LAUNCH_PATCH_FUNCTION REPLACE_AREA_ITEM
    STR_VAR
    old_item		= "%tutu_var%sw1h24"
    new_item		= "mh#dagg2"
  END

  // Add Trollkiller +3
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 36
    STR_VAR
    item_to_add		= "mh#btax2"
  END

  // Add green dragon scales
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 6
    STR_VAR
    item_to_add		= "mh#misc1"
  END


// Wizard's Hat on King

COPY_EXISTING "%tutu_scriptbg%king.cre" "override"
  ADD_CRE_ITEM "mh#helm2" #0 #0 #0 "none" "helmet"


// Girdle of Cloud Giant strength on Grael

COPY_EXISTING "%tutu_var%grael.cre" "override"
  ADD_CRE_ITEM "mh#belt2" #0 #0 #0 "none" "belt"


// Move Cloak of the Shield to Firewine

COPY_EXISTING "%tutu_scripto%grema02.cre" "override"
  ADD_CRE_ITEM "%tutu_var%clck20" #20 #0 #0 "none" "cloak"


// Oobah & Blahr encounter at Candlekeep for moved Krotan's Skullcrusher

COPY_LARGE "%MOD_FOLDER%/item_pack/bgt/bitmaps/mh#oobal.bmp" "override"
           "%MOD_FOLDER%/item_pack/bgt/bitmaps/mh#oobas.bmp" "override"
           "%MOD_FOLDER%/item_pack/bgt/bitmaps/mh#blahl.bmp" "override"
           "%MOD_FOLDER%/item_pack/bgt/bitmaps/mh#blahs.bmp" "override"


COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#oobah.cre" "override"
  SAY NAME1 "Oobah"
  SAY NAME2 "Oobah"

  PATCH_IF ENGINE_IS "bg1 totsc"
  BEGIN
    WRITE_BYTE	0x0072	4	// Blunt weapons
    WRITE_BYTE	0x0073	4	// Spiked weapons
  END
  ELSE
  BEGIN
    SET_BG2_PROFICIENCY	PROFICIENCYMACE 4
    SET_BG2_PROFICIENCY	PROFICIENCYFLAILMORNINGSTAR 4
  END


COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#blahr.cre" "override"
  SAY NAME1 "Blahr"
  SAY NAME2 "Blahr"

  PATCH_IF ENGINE_IS "bg1 totsc"
  BEGIN
    WRITE_BYTE	0x0072	4	// Blunt weapons
    WRITE_BYTE	0x0073	4	// Spiked weapons
  END
  ELSE
  BEGIN
    SET_BG2_PROFICIENCY	PROFICIENCYMACE 4
    SET_BG2_PROFICIENCY	PROFICIENCYFLAILMORNINGSTAR 4
  END


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/item_pack/bgt/dialog/oobah_blahr.d"
END


LAUNCH_ACTION_FUNCTION cd_extend_bg_area_script
  STR_VAR
  area			= "%Candlekeep_Ch6%"
  script		= "%MOD_FOLDER%/item_pack/bgt/scripts/bg2626"
END


// ======================================
// Erase Journal Entries when BGT is over
// ======================================

ACTION_FOR_EACH dialog IN "mh#brok1" "mh#helen" "mh#letha" "mh#tania"
BEGIN
  LAUNCH_ACTION_FUNCTION erase_journal_entries_on_bg2_transition
    STR_VAR
    dialog
  END
END


