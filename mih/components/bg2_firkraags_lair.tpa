// Enhanced Firkraag's Lair


// Spawn group for (greater) basilisks
APPEND_COL "spawngrp.2da" "$ $ RDMHbasl 60 basles01 basles01 basgrt01 * * * * *"

// Spawn group for ankhegs
APPEND_COL "spawngrp.2da" "$ $ RDMHankh 60 ankheg01 ankheg01 ankheg01 ankheg01 ankheg01 * * *"

// Make sure spawn table is properly formatted
COPY_EXISTING "spawngrp.2da" "override"
  PRETTY_PRINT_2DA

  BUT_ONLY_IF_IT_CHANGES


// Windspear Hills

COPY "%MOD_FOLDER%/bg2/firkraags_lair/mh#flpdw.cre" "override"
  SAY NAME1 "Brundul"
  SAY NAME2 "Brundul"


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/bg2/firkraags_lair/mh#flpdw.d"
END


COPY "%MOD_FOLDER%/bg2/firkraags_lair/mh#flpel.cre" "override"
  SAY NAME1 "Ailluin"
  SAY NAME2 "Ailluin"


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/bg2/firkraags_lair/mh#flpel.d"
END


COPY_EXISTING "ar1200.are" "override"
  // Add new actors
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg2/firkraags_lair/ar1200.2da"
  END

  // Alter spawn points
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0074) 10
    WRITE_SHORT (spawn_offset + 0x0084) 6

    WRITE_ASCII (spawn_offset + 0x0024) "RDMHbasl" #8
    WRITE_ASCII (spawn_offset + 0x002c) "RDMHankh" #8
    WRITE_ASCII (spawn_offset + 0x0034) "RDGnoll" #8
    WRITE_ASCII (spawn_offset + 0x003c) "RDOrc" #8
    WRITE_ASCII (spawn_offset + 0x0044) "RDOrog" #8
    WRITE_ASCII (spawn_offset + 0x004c) "RDOgre" #8
    WRITE_ASCII (spawn_offset + 0x0054) "RDHobgob" #8
    WRITE_ASCII (spawn_offset + 0x005c) "RDLiz" #8
    WRITE_ASCII (spawn_offset + 0x0064) "bearca01" #8
    WRITE_ASCII (spawn_offset + 0x006c) "bearpo01" #8
  END


// Mist Cavern

COPY_EXISTING "ar1201.are" "override"
  // Alter spawn points
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0074) 2
    WRITE_SHORT (spawn_offset + 0x0084) 3

    WRITE_ASCII (spawn_offset + 0x0024) "RDMist1" #8
    WRITE_ASCII (spawn_offset + 0x002c) "RDMist2" #8
  END


// Main Complex

COPY_EXISTING "ar1202.are" "override"
  // Alter spawn points (just increase maximum #)
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0084) (THIS + 3)
  END

  // Add new high level trap
  LAUNCH_PATCH_FUNCTION ADD_AREA_REGION_TRIGGER
    INT_VAR
    ab_RT_BbLX		= 3258
    ab_RT_BbLY		= 461
    ab_RT_BbHX		= 3382
    ab_RT_BbHY		= 545
    ab_RT_VxPr		= 4
    ab_RT_Vx_X_0	= 3382
    ab_RT_Vx_Y_0	= 525
    ab_RT_Vx_X_1	= 3352
    ab_RT_Vx_Y_1	= 545
    ab_RT_Vx_X_2	= 3258
    ab_RT_Vx_Y_2	= 480
    ab_RT_Vx_X_3	= 3285
    ab_RT_Vx_Y_3	= 461
    ab_RT_TDtD		= 100
    ab_RT_TRmD		= 100
    ab_RT_TSet		= 1
    ab_RT_LPoX		= 3245
    ab_RT_LPoY		= 175

    STR_VAR
    ab_RT_Name		= "MiH#HighLevel3"
    ab_RT_Rbcs		= "spwndead"
  END


