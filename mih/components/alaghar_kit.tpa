// Alaghar of Clangeddin kit


INCLUDE "%MOD_FOLDER%/lib/generic_effects.tpa"


// BAM files

COPY_LARGE "%MOD_FOLDER%/alaghar_kit/mh#cssym.bam" "override"


// Pseudo-spells

COPY "%MOD_FOLDER%/alaghar_kit/mh#att20.spl" "override"
     "%MOD_FOLDER%/alaghar_kit/mh#att32.spl" "override"
     "%MOD_FOLDER%/alaghar_kit/mh#csund.spl" "override"
     "%MOD_FOLDER%/alaghar_kit/mh#cssym.spl" "override"


// ===========
// Silverbeard
// ===========

COPY "%MOD_FOLDER%/alaghar_kit/mh#silbd.spl" "override"
  SAY NAME1 "Silverbeard"
  SAY UNIDENTIFIED_DESC "Silverbeard (Alteration)
Level: 1
Sphere: Combat
Range: 0
Duration: 1 turn + 1 round/level
Casting Time: 4
Area of Effect: Caster
Saving Throw: None

By means of this spell, the priest temporarily transforms his or her beard into refined silver.  In addition to the stunning visual impact (charisma +2) of this spell effect, a Silverbeard serves as an unusual shield in combat situations.

If the priest is unarmored, Silverbeard confers a base AC of 8.  If armor of any sort is worn, this spell confers a +1 AC bonus for the duration of the spell.  This bonus improves by +1 for every 5 levels of the caster, up to a bonus of +5 at level 20.

This spell is only available to clerics of the dwarven god Clangeddin Silverbeard."

  LAUNCH_PATCH_FUNCTION prevent_spell_effect_stacking END

  PATCH_IF FILE_EXISTS_IN_GAME "icarmor.vvc"
  BEGIN
    LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
      match_opcode	= 142	// Graphics: Display SFX Icon
      opcode		= 215	// Graphics: Play 3D Effect
      parameter1	= 0
      parameter2	= 1
      duration		= 2
      STR_VAR
      resource		= "icarmor"
      insert		= "below"
    END
  END


// =========
// Rockburst
// =========

COPY "%MOD_FOLDER%/alaghar_kit/mh#rockb.spl" "override"
  SAY NAME1 "Rockburst"
  SAY UNIDENTIFIED_DESC "Rockburst (Alteration)
Level: 2
Sphere: Combat
Range: 20 yards
Duration: Instantaneous
Casting Time: 5
Area of Effect: 12-foot radius
Saving Throw: 1/2

This spell allows the caster to make a boulder or rockpile explode suddenly, propelling jagged shards in all directions.  The shrapnel endangers all beings within 12 feet of the detonation.  The shards deal 1d4 +1 points of damage per level of the caster, up to a maximum of 10d4 +10.  A saving throw versus spell cuts the damage in half.

This spell is only available to clerics of the dwarven god Clangeddin Silverbeard."


// =======================
// Axe Storm of Clangeddin
// =======================


COPY "%MOD_FOLDER%/alaghar_kit/mh#axest.spl" "override"
  SAY NAME1 "Axe Storm of Clangeddin"
  SAY UNIDENTIFIED_DESC "Axe Storm of Clangeddin (Alteration)
Level: 4
Sphere: Combat
Range: 0
Duration: 3 rounds
Casting Time: 7
Area of Effect: 15-foot radius
Saving Throw: None

For the next three rounds after this spell has been cast, the caster and all nearby allies can make three attacks per round.  Movement and spellcasting are not affected.  This spell is not cumulative with itself or with similar magic like Haste.

This spell is only available to clerics of the dwarven god Clangeddin Silverbeard."


// =====================================
// Holy Symbol of Clangeddin Silverbeard
// =====================================

COPY "%MOD_FOLDER%/alaghar_kit/mh#cssym.itm" "override"
  SAY NAME2 "Holy Symbol of Clangeddin"
  SAY IDENTIFIED_DESC "Holy Symbol of Clangeddin Silverbeard

Only the most worthy and devoted servants of the Father of Battle are granted these powerful holy symbols.

STATISTICS:

   Grants an additional 6th and 7th level spell.
   5% Magic Resistance while worn
   +1 to Strength while worn

Only usable by:
 Lawful Good or Lawful Neutral Dwarven Cleric"


// =====================
// Alaghar of Clangeddin
// =====================

ADD_KIT "mh#clangeddin"
  // clasweap.2da (BG1-style proficiencies)
  "mh#clangeddin	0	0	1	1	0	1	0	0"

  // weapprof.2da (BG2-style proficiencies)
  "mh#clangeddin	0 0 0 0 2 2 0 2 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0"

  // abclasrq.2da (minimum stat requirements)
  "mh#clangeddin	15	0	0	0	9	0"

  // abclsmod.2da (stat modifications)
  "mh#clangeddin	0	0	0	0	0	0"

  // abdcdsrq.2da (stat requirements to dual-class to this kit)
  "mh#clangeddin	15	0	0	0	17	0"

  // abdcscrq.2da (stat requirements to dual-class from this kit)
  "mh#clangeddin	15	0	0	0	15	0"

  // alignmnt.2da (allowed aligments for this kit)
  "mh#clangeddin	1	1	0	0	0	0	0	0	0"

  // dualclas.2da (allowed classes to dual-class with)
  "mh#clangeddin	1	0	0	0	0	0"

  // path to class abilities table
  "%MOD_FOLDER%/alaghar_kit/mh#clang.2da"

  // allowed PC races and classes
  "K_C_D"

  // item usability flags
  "0x00004000	3"

  // luabbr.2da (ToB HLA table)
  "Cl0"

  // 25stweap.2da (ToB starting equipment)
  "CHAN09 * HELM07 BAG20 RING06 RING31 * BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN4,2 POTN14,5 HAM07 SW1H27 STAF08"

  // kit name/description strings
  SAY "Alaghar of Clangeddin"
  SAY "alaghar of clangeddin"
  SAY "ALAGHAR OF CLANGEDDIN: Clangeddin Silverbeard (CLAN-gehd-din SIHL-vur-beerd) is the Father of Battle and the primary dwarven war god.  All dwarves who must fight, especially dwarves who are warriors by profession, worship Clangeddin, their patron and exultant leader in war.

Clangeddin encourages valor in battle, weapon-mastery and training, and wisdom in war.  He especially hates giants and has taught the dwarves special ways to fight giant-type creatures.  Clangeddin is a resolute warrior who never backs down from danger and who refuses to surrender even when all seems lost.  He is a strict and ethical deity who brooks no treachery or deceit.

Clangeddin's specialty priests are known as the Alaghor (plural form of Alaghar) of Clangeddin.  They are revered throughout dwarven culture for their dedication and skill in battle.  Other races often see them as bloodthirdty berserkers, but those who fight alongside them quickly learn of their principled approach to warfare and the lengths they will go to defend their fellow dwarves and allies.

Advantages:
- May place up to 2 proficiency points in weapons and maximum in fighting styles.
- Alaghor strike undead creatures with +2 to hit and damage.
- Alaghor can memorize and cast the unique spells Silverbeard, Rockburst and Axe Storm of Clangeddin at appropriate levels (1st, 3rd and 7th, respectively).
- May cast Strength (self only) once per day every 5 levels (starts at 1st level with one use).
- At 7th level, alaghor make at least three attacks every two rounds with any weapon they can wield.  (This is not cumulative with weapon specialization.)
- At 13th level, alaghor make at least two attacks per round with any weapon they can wield.

Disadvantages:
- Race restricted to dwarf.
- Alignment restricted to lawful good or lawful neutral.
- Alaghor cannot turn undead."


