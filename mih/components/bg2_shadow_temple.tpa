// Enhanced Shadow Temple

LAUNCH_ACTION_FUNCTION install_component
  STR_VAR
  component_name	= "mih_monster_pack"
END


// Spawn group for shadow wolves
APPEND_COL "spawngrp.2da" "$ $ RDMHshwl 60 shawol01 shawol01 shawol01 shawol02 shawol01 shawol02 shawol02 *"

// Make sure spawn table is properly formatted
COPY_EXISTING "spawngrp.2da" "override"
  PRETTY_PRINT_2DA

  BUT_ONLY_IF_IT_CHANGES


// Temple Interior

COPY_EXISTING "ar1401.are" "override"
  // Add new actors
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg2/shadow_temple/ar1401.2da"
  END

  // Alter spawn points
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0074) 3
    WRITE_SHORT (spawn_offset + 0x0084) 5

    WRITE_ASCII (spawn_offset + 0x0024) "RDMHshwl" #8
    WRITE_ASCII (spawn_offset + 0x002c) "RDUndea2" #8
    WRITE_ASCII (spawn_offset + 0x0034) "RDShad" #8
  END

  // Fix unlogical launch point of first high-level trap
  LAUNCH_PATCH_FUNCTION ALTER_AREA_REGION
    INT_VAR
    launch_x		= 745
    launch_y		= 350
    STR_VAR
    region_name		= "High Level"
  END

  // Add new high level trap
  LAUNCH_PATCH_FUNCTION ADD_AREA_REGION_TRIGGER
    INT_VAR
    ab_RT_BbLX		= 1640
    ab_RT_BbLY		= 1419
    ab_RT_BbHX		= 1707
    ab_RT_BbHY		= 1493
    ab_RT_VxPr		= 4
    ab_RT_Vx_X_0	= 1694
    ab_RT_Vx_Y_0	= 1493
    ab_RT_Vx_X_1	= 1640
    ab_RT_Vx_Y_1	= 1485
    ab_RT_Vx_X_2	= 1667
    ab_RT_Vx_Y_2	= 1419
    ab_RT_Vx_X_3	= 1707
    ab_RT_Vx_Y_3	= 1429
    ab_RT_TDtD		= 100
    ab_RT_TRmD		= 100
    ab_RT_TSet		= 1
    ab_RT_LPoX		= 2433
    ab_RT_LPoY		= 1288

    STR_VAR
    ab_RT_Name		= "MiH#HighLevel2"
    ab_RT_Rbcs		= "spwndead"
  END


// Corrupted Temple Grounds

COPY_EXISTING "ar1404.are" "override"
  // Alter spawn points
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0074) 3
    WRITE_SHORT (spawn_offset + 0x0084) 5

    WRITE_ASCII (spawn_offset + 0x0024) "RDMHshwl" #8
    WRITE_ASCII (spawn_offset + 0x002c) "RDUndea2" #8
    WRITE_ASCII (spawn_offset + 0x0034) "RDShad" #8
  END


