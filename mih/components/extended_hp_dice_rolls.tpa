// Extended HP Dice Rolls
//
// In AD&D 2E, characters stopped rolling for HP after level 9.
// This was extended to level 20 in AD&D 3E.  This mod implements
// the 3E rule.


ACTION_CLEAR_ARRAY hp_tables

ACTION_IF FILE_EXISTS_IN_GAME "hpclass.2da"
BEGIN
  // hpclass.2da found (ToBex or BG(2)EE, read hp tables from it.
  COPY_EXISTING - "hpclass.2da" "override"
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows

    FOR (i = 0; i < rows; ++i)
    BEGIN
      READ_2DA_ENTRY i 1 cols table

      PATCH_DEFINE_ASSOCIATIVE_ARRAY hp_tables     
      BEGIN
        "%table%" => 1
      END
    END
END
ELSE
BEGIN
  // No hpclass.2da (not ToBex or BG(2)EE), use a static table
  ACTION_DEFINE_ASSOCIATIVE_ARRAY hp_tables     
  BEGIN
    "hpwar" => 1
    "hpprs" => 1
    "hprog" => 1
    "hpwiz" => 1
    "hpmonk" => 1
    "hpbarb" => 1
  END
END

ACTION_PHP_EACH hp_tables AS table => int
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%table%.2da"
  BEGIN
    COPY_EXISTING - "%table%.2da" "override"

    SET even_levels_sides = 0
    SET odd_levels_sides = 0

    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows

    CLEAR_ARRAY __hp_table_read
    READ_2DA_ENTRIES_NOW __hp_table_read cols

    FOR (i = 0; i < rows; ++i)
    BEGIN
      READ_2DA_ENTRY_FORMER __hp_table_read i 0 level
      READ_2DA_ENTRY_FORMER __hp_table_read i 1 sides
      READ_2DA_ENTRY_FORMER __hp_table_read i 2 rolls
      READ_2DA_ENTRY_FORMER __hp_table_read i 3 bonus

      PATCH_IF level == 8
      BEGIN
        even_level_sides = sides
        even_level_rolls = rolls
        even_level_bonus = bonus
      END

      PATCH_IF level == 9
      BEGIN
        odd_level_sides = sides
        odd_level_rolls = rolls
        odd_level_bonus = bonus
      END
    END

    ACTION_IF (even_level_sides && odd_level_sides)
    BEGIN
      COPY_EXISTING "%table%.2da" "override"
        COUNT_2DA_COLS cols
        COUNT_2DA_ROWS cols rows

        CLEAR_ARRAY __hp_table_read
        READ_2DA_ENTRIES_NOW __hp_table_read cols

        FOR (i = 0; i < rows; ++i)
	BEGIN
          READ_2DA_ENTRY_FORMER __hp_table_read i 0 level
      
          PATCH_IF (level > 9) && (level <= 20)
	  BEGIN
	    PATCH_IF (level & BIT0)
	    BEGIN
	      SET_2DA_ENTRY_LATER __hp_table_write i 1 odd_level_sides
	      SET_2DA_ENTRY_LATER __hp_table_write i 2 odd_level_rolls
	      SET_2DA_ENTRY_LATER __hp_table_write i 3 odd_level_bonus
	    END
	    ELSE
	    BEGIN
	      SET_2DA_ENTRY_LATER __hp_table_write i 1 even_level_sides
	      SET_2DA_ENTRY_LATER __hp_table_write i 2 even_level_rolls
	      SET_2DA_ENTRY_LATER __hp_table_write i 3 even_level_bonus
	    END
	  END
	END

	SET_2DA_ENTRIES_NOW __hp_table_write cols
	PRETTY_PRINT_2DA
	BUT_ONLY_IF_IT_CHANGES
    END
  END
END


