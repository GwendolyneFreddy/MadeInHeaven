// Enhanced Candlekeep Catacombs


LAUNCH_ACTION_FUNCTION install_component
  STR_VAR
  component_name	= "mih_monster_pack"
END


// Restored Erik doppleganger

COPY "%MOD_FOLDER%/bg1/candlekeep_catacombs/mh#erik3.cre" "override"
  SAY NAME1 "Erik"
  SAY NAME2 "Erik"


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/bg1/candlekeep_catacombs/mh#erik3.d"
END


// Original Priest of Oghma doppleganger

COPY "%MOD_FOLDER%/bg1/candlekeep_catacombs/mh#doppr.cre" "override"
  SAY NAME1 "Priest of Oghma"
  SAY NAME2 "Priest of Oghma"


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/bg1/candlekeep_catacombs/mh#doppr.d"
END


// Original Keeper of the Portal doppleganger

COPY "%MOD_FOLDER%/bg1/candlekeep_catacombs/mh#dopkp.cre" "override"
  SAY NAME1 "Keeper of the Portal"
  SAY NAME2 "Keeper of the Portal"


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/bg1/candlekeep_catacombs/mh#dopkp.d"
END


// Area Edits

COPY_EXISTING "%Candlekeep_Catacombs_L1%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    bgt_compat		= 1
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/candlekeep_catacombs/bg2615.2da"
  END


COPY_EXISTING "%Candlekeep_Catacombs_L2%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/candlekeep_catacombs/bg2619.2da"
  END


COPY_EXISTING "%CandlekeepCaves%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    bgt_compat		= 1
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/candlekeep_catacombs/bg5506.2da"
  END


// Improved Arkanis and Deder

COPY_EXISTING "%tutu_var%arkani3.cre" "override"
  // Equipment
  REPLACE_CRE_ITEM "%tutu_var%ax1h02" #0 #0 #0 "none" "weapon1" EQUIP
  REPLACE_CRE_ITEM "%tutu_var%hamm02" #0 #0 #0 "none" "weapon2"
  REPLACE_CRE_ITEM "%tutu_var%shld06" #0 #0 #0 "none" "shield" EQUIP
  REPLACE_CRE_ITEM "%tutu_var%chan02" #0 #0 #0 "none" "armor"
  REPLACE_CRE_ITEM "%tutu_var%helm08" #0 #0 #0 "none" "helmet"
  REPLACE_CRE_ITEM "%tutu_var%potn08" #5 #0 #0 "none" "qitem1"
  REPLACE_CRE_ITEM "%tutu_var%potn09" #1 #0 #0 "none" "qitem2"
  REPLACE_CRE_ITEM "%tutu_var%potn12" #1 #0 #0 "none" "qitem3"
  REMOVE_CRE_ITEM "%tutu_var%iarow01"

  // Proficiencies
  PATCH_IF ENGINE_IS "bg1 totsc"
  BEGIN
    WRITE_BYTE	0x0072	2	// Blunt prof.
    WRITE_BYTE	0x0074	3	// Axe prof.
    WRITE_BYTE	0x0075	0	// Missile prof.
    WRITE_SHORT	0x004a	"-4"	// Simulate sword&shield
  END
  ELSE
  BEGIN
    WRITE_BYTE	0x0074	0	// Axe prof.
    WRITE_BYTE	0x0075	0	// Missile prof.
    SET_BG2_PROFICIENCY	PROFICIENCYAXE			3
    SET_BG2_PROFICIENCY	PROFICIENCYWARHAMMER		2
    SET_BG2_PROFICIENCY	PROFICIENCYSWORDANDSHIELD	2
  END

  // Stats
  WRITE_LONG	0x0014	975	// XP value
  WRITE_SHORT	0x0024	56	// Current HP
  WRITE_SHORT	0x0026	56	// Maximum HP
  WRITE_BYTE	0x0053	7	// # of attacks 3/2
  WRITE_BYTE	0x0054	8	// Save vs. Death
  WRITE_BYTE	0x0055	10	// Save vs. Wands
  WRITE_BYTE	0x0056	9	// Save vs. Poly
  WRITE_BYTE	0x0057	9	// Save vs. Breath
  WRITE_BYTE	0x0058	11	// Save vs. Spell
  WRITE_BYTE	0x0234	9	// Level
  WRITE_BYTE	0x0273	IDS_OF_SYMBOL("class" "fighter")


COPY_EXISTING "%tutu_var%deder3.cre" "override"
  // Equipment
  REPLACE_CRE_ITEM "%tutu_var%bow06" #0 #0 #0 "none" "weapon1" EQUIP
  REPLACE_CRE_ITEM "%tutu_var%sw1h08" #0 #0 #0 "none" "weapon2"
  REPLACE_CRE_ITEM "%tutu_var%arow02" #20 #0 #0 "none" "quiver1"
  REPLACE_CRE_ITEM "%tutu_var%arow02" #20 #0 #0 "none" "quiver2"
  REPLACE_CRE_ITEM "%tutu_var%arow02" #20 #0 #0 "none" "quiver3"
  REPLACE_CRE_ITEM "%tutu_var%leat05" #0 #0 #0 "none" "armor"
  REPLACE_CRE_ITEM "%tutu_var%potn08" #5 #0 #0 "none" "qitem1"
  REPLACE_CRE_ITEM "%tutu_var%potn10" #2 #0 #0 "none" "qitem2"
  REPLACE_CRE_ITEM "%tutu_var%potn13" #1 #0 #0 "none" "qitem3"

  // Proficiencies
  PATCH_IF ENGINE_IS "bg1 totsc"
  BEGIN
    WRITE_BYTE	0x006f	2	// Small sword prof.
    WRITE_BYTE	0x0075	2	// Missile prof.
    WRITE_SHORT	0x004a	"-2"	// Simulate single weapon
    WRITE_SHORT	0x004e	"-2"	// Simulate single weapon
    WRITE_SHORT	0x0050	"-2"	// Simulate single weapon
  END
  ELSE
  BEGIN
    WRITE_BYTE	0x006f	0	// Small sword prof.
    WRITE_BYTE	0x0075	0	// Missile prof.
    SET_BG2_PROFICIENCY	PROFICIENCYSHORTSWORD		2
    SET_BG2_PROFICIENCY	PROFICIENCYSHORTBOW		2
    SET_BG2_PROFICIENCY	PROFICIENCYSINGLEWEAPON		2
  END

  // Stats
  WRITE_LONG	0x0014	975	// XP value
  WRITE_SHORT	0x0024	42	// Current HP
  WRITE_SHORT	0x0026	42	// Maximum HP
  WRITE_BYTE	0x0053	7	// # of attacks 3/2
  WRITE_BYTE	0x0054	10	// Save vs. Death
  WRITE_BYTE	0x0055	12	// Save vs. Wands
  WRITE_BYTE	0x0056	11	// Save vs. Poly
  WRITE_BYTE	0x0057	12	// Save vs. Breath
  WRITE_BYTE	0x0058	13	// Save vs. Spell
  WRITE_BYTE	0x0065	50	// Set traps
  WRITE_BYTE	0x0068	70	// Stealth
  WRITE_BYTE	0x0069	70	// Find/disarm traps
  WRITE_BYTE	0x0234	7	// Level (fighter)
  WRITE_BYTE	0x0235	9	// Level (thief)
  WRITE_BYTE	0x0273	IDS_OF_SYMBOL("class" "fighter_thief")

  PATCH_IF FILE_EXISTS_IN_GAME "spcl412.spl"
  BEGIN
    ADD_MEMORIZED_SPELL "spcl412" #0 "innate" (2)
  END


ADD_SPELL "%MOD_FOLDER%/bg1/candlekeep_catacombs/player_control.spl" 3 1 MH_PLAYER_CONTROL


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/bg1/candlekeep_catacombs/arkanis.d"
END


