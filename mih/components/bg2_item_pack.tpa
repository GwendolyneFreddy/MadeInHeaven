// MiH Item Pack for BG1

LAUNCH_ACTION_FUNCTION "install_component"
  STR_VAR
  component_name	= "mih_item_pack"
END


// =============
// Brokk's Forge
// =============

COPY "%MOD_FOLDER%/item_pack/bgt/actors/mh#brok2.cre" "override"
  SAY NAME1 "Brokk"
  SAY NAME1 "Brokk"


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/item_pack/bgt/dialog/mh#brok2.d"
END


COPY "%MOD_FOLDER%/item_pack/bgt/stores/mh#brok2.sto" "override"
  SAY NAME2 "Brokk's Forge"


EXTEND_BOTTOM "ar2000.bcs" "%MOD_FOLDER%/item_pack/bgt/scripts/brokk_trademeet.baf" USING "%MOD_FOLDER%/dummy.tra"


// ================
// Chateau Irenicus
// ================

// First floor
COPY_EXISTING "ar0602.are" "override"
  // "starter" rings in chest with portal key
  PATCH_FOR_EACH item_to_add IN "mh#ring1" "mh#ring2" "mh#ring3"
  BEGIN
    LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
      INT_VAR
      container_to_add_to	= 35
      STR_VAR
      item_to_add
    END
  END

  // Bag of Tricks in barrel near portal
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 6
    STR_VAR
    item_to_add			= "mh#misc4"
  END


// Escaped Clone
COPY_EXISTING "clone1.cre" "override"
  // Mage Masher +1
  REPLACE_CRE_ITEM "mh#dagg1" #0 #0 #0 "unstealable" "weapon1" EQUIP


// ===================
// Waukeen's Promenade
// ===================

// Arnolius' Armor Store

COPY_EXISTING "shop02.sto" "override"
  // Winter Wolf Hide +1
  ADD_STORE_ITEM "mh#armr3" AFTER "leat10" #0 #0 #0 "identified" #1
  // Shield of the Phoenix +1
  ADD_STORE_ITEM "mh#shld1" AFTER "shld30" #0 #0 #0 "identified" #1
  // Helm of Valor
  ADD_STORE_ITEM "mh#helm1" AFTER "helm01" #0 #0 #0 "identified" #1


// Maheer's Weapon Store

COPY_EXISTING "shop03.sto" "override"
  // Slime Crusher +1
  ADD_STORE_ITEM "mh#blun1" AFTER "blun06" #0 #0 #0 "identified" #1

  // Heartsblood +1
  ADD_STORE_ITEM "mh#swrd2" AFTER "sw1h04" #0 #0 #0 "identified" #1


// Ribald's Adventurer's Market

COPY_EXISTING "ribald.sto" "override"
  ADD_STORE_ITEM "mh#potn1" AFTER "potn12 potn10 potn09" #1 #0 #0 "identified" #2
  ADD_STORE_ITEM "mh#potn2" AFTER "potn35 potn45 potn20" #1 #0 #0 "identified" #2


// ===============
// Temple District
// ===============

// Temple of Lathander

COPY_EXISTING "temlath.sto" "override"
  // Amulet against Undead
  ADD_STORE_ITEM "mh#amul1" LAST #0 #0 #0 "identified" #1

  // Gem of Insight
  ADD_STORE_ITEM "mh#amul2" LAST #1 #0 #0 "identified" #1

  // Necklace of Prayer Beads
  ADD_STORE_ITEM "mh#amul3" LAST #3 #1 #1 "identified" #1

  // Periapt of Health
  ADD_STORE_ITEM "mh#amul4" LAST #0 #0 #0 "identified" #1

  // Blessed Mace +2
  ADD_STORE_ITEM "mh#blun2" AFTER "staf05" #0 #0 #0 "identified" #1


// Tarnor the Hatchetman

COPY_EXISTING "tarnor.cre" "override"
  // Foesplitter +1
  REPLACE_CRE_ITEM "mh#btax1" #0 #0 #0 "unstealable" "weapon2" EQUIP


// =============
// Planar Sphere
// =============

COPY_EXISTING "ar0411.are" "override"
  // Sword of the Planes on table near knights
  LAUNCH_PATCH_FUNCTION ADD_AREA_ITEM
    INT_VAR
    container_to_add_to	= 8
    STR_VAR
    item_to_add			= "mh#swrd6"
  END


// =========
// Trademeet
// =========

// Nobleman merchant

COPY_EXISTING "trmer02.sto" "override"
  // Catclaw +3
  ADD_STORE_ITEM "mh#dagg2" AFTER "dagg15" #0 #0 #0 "identified" #1

  // Terminus Est +2
  ADD_STORE_ITEM "mh#swrd1" AFTER "sw1h02" #0 #0 #0 "identified" #1

  // Mutamin's Cloak
  ADD_STORE_ITEM "mh#clck1" AFTER "clck03" #0 #0 #0 "identified" #1


// =============
// Druid's Grove
// =============

// Dalok

COPY_EXISTING "cedruid2.cre" "override"
  // Lush Green Ioun Stone
  REPLACE_CRE_ITEM "mh#ioun1" #0 #0 #0 "unstealable" "helmet"


// Adherent

COPY_EXISTING "cefgt01.cre" "override"
  // Selune's Blessing
  REPLACE_CRE_ITEM "mh#swrd3" #0 #0 #0 "unstealable" "weapon1" EQUIP


// ==========
// Umar Hills
// ==========

// Imnesvale Merchant

COPY_EXISTING "uhmer01.sto" "override"
  // Bone Ring
  ADD_STORE_ITEM "mh#ring7" AFTER "amul10" #12 #0 #0 "identified" #1

  // Oil of Acid Resistance
  ADD_STORE_ITEM "mh#potn1" AFTER "potn02" #1 #0 #0 "identified" #4


// ===============
// Windspear Hills
// ===============

// Firkraag

COPY_EXISTING "firkra02.cre" "override"
  REPLACE_CRE_ITEM "mh#clck2" #0 #0 #0 "none" "inv2"


// =========
// Underdark
// =========

// Therndle Daglefodd (reward for returning lost son)
//
// (He uses GiveItemCreate() and lucky for me, with the right number
//  of charges too.  So I only need to patch his dialog.)

COPY_EXISTING "udsvir05.dlg" "override"
  DECOMPILE_AND_PATCH
  BEGIN
    // Bracers of Winter's Chill (replaces Bracers of Defense)
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "brac14" "mh#brac1"
  END


// Drow Merchants

COPY_EXISTING "uddrow23.sto" "override"
  // Ring of Mind Shielding
  ADD_STORE_ITEM "mh#ring5" AFTER "staf16" #0 #0 #0 "identified" #1

  // Cloak of Arachnida
  ADD_STORE_ITEM "mh#clck3" AFTER "clck17" #2 #0 #0 "identified" #1


COPY_EXISTING "uddrow24.sto" "override"
  // Boots of Gravewalking
  ADD_STORE_ITEM "mh#boot3" AFTER "rods04" #1 #0 #0 "identified" #1

  // Oil of Acid Resistance
  ADD_STORE_ITEM "mh#potn1" AFTER "potn02" #1 #0 #0 "identified" #3

  // Oil of Disenchantment
  ADD_STORE_ITEM "mh#potn2" AFTER "potn35" #1 #0 #0 "identified" #1


COPY_EXISTING "uddrow25.sto" "override"
  // Shroudcrown
  ADD_STORE_ITEM "mh#helm4" FIRST #1 #0 #0 "identified" #1


// ==============
// Watcher's Keep
// ==============

// Sister Garlena

COPY_EXISTING "garlena.sto" "override"
  // Sunite Sash
  ADD_STORE_ITEM "mh#belt3" LAST #0 #0 #0 "identified" #1

  // Feather Tabi
  ADD_STORE_ITEM "mh#boot1" LAST #0 #0 #0 "identified" #1

  // Boots of Striding & Springing
  ADD_STORE_ITEM "mh#boot2" LAST #0 #0 #0 "identified" #1

  // Gauntlets of the Dwarven King
  ADD_STORE_ITEM "mh#brac2" LAST #0 #0 #0 "identified" #1


// =========
// Amkethran
// =========

// Arcana Archives

COPY_EXISTING "25spell.sto" "override"
  // Bracers of Defense AC 2
  ADD_STORE_ITEM "mh#brac3" AFTER "clck11" #0 #0 #0 "identified" #1

  // Ring of Anti-Venom (moved from Vongoethe)
  ADD_STORE_ITEM "ring46" AFTER "ring02" #0 #0 #0 "identified" #1

  // Ring of Shooting Stars
  ADD_STORE_ITEM "mh#ring6" AFTER "ring46" #3 #2 #1 "identified" #1

  // Pearl of Power
  ADD_STORE_ITEM "mh#ioun2" AFTER "helm23" #0 #0 #0 "identified" #1


// Vongoethe the lich

COPY_EXISTING "amlich01.cre" "override"
  // Ring of Protection +3
  REPLACE_CRE_ITEM "mh#ring4" #0 #0 #0 "none" "lring"


// ============================
// New Potions in various shops
// ============================

COPY_EXISTING "murcrag.sto" "override"
              "roger.sto" "override"
              "temhelm.sto" "override"
  ADD_STORE_ITEM "mh#potn1" AFTER "potn12 potn10 potn09" #1 #0 #0 "identified" #3
  ADD_STORE_ITEM "mh#potn2" AFTER "potn35 potn45 potn20" #1 #0 #0 "identified" #1


