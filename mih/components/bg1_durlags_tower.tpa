// Enhanced Durlag's Tower

LAUNCH_ACTION_FUNCTION install_component
  STR_VAR
  component_name	= "mih_monster_pack"
END


// Give unique Durlag's Tower ghast a power boost

COPY_EXISTING "%tutu_var%ghastd.cre" "override"
  SAY NAME1 "Evolved Ghast"
  SAY NAME2 "Evolved Ghast"

  ADD_CRE_ITEM "immune1" #0 #0 #0 "unstealable&undroppable" "ring"

  WRITE_LONG	0x0014	1400		// XP value
  WRITE_SHORT	0x0024	(THIS * 2)	// Current HP
  WRITE_SHORT	0x0026	(THIS * 2)	// Maximum HP
  WRITE_SHORT	0x0046	(THIS - 2)	// Natural AC
  WRITE_SHORT	0x0048	(THIS - 2)	// Effective AC
  WRITE_BYTE	0x0052	(THIS - 2)	// THAC0
  WRITE_BYTE	0x0053	(THIS + 1)	// APR
  WRITE_BYTE	0x0054	(THIS - 1)	// Save vs. death
  WRITE_BYTE	0x0055	(THIS - 1)	// Save vs. wands
  WRITE_BYTE	0x0056	(THIS - 1)	// Save vs. poly
  WRITE_BYTE	0x0057	(THIS - 1)	// Save vs. breath
  WRITE_BYTE	0x0058	(THIS - 1)	// Save vs. spell
  WRITE_BYTE	0x005d	(THIS + 20)	// MR
  WRITE_BYTE	0x0234	(THIS + 2)	// Level
  WRITE_BYTE	0x0238	(THIS + 2)	// Strength
  WRITE_BYTE	0x023d	(THIS + 2)	// Charisma


// New spawn groups for games supporting it

ACTION_IF FILE_EXISTS_IN_GAME "spawngrp.2da"
BEGIN
  APPEND_COL "spawngrp.2da" "$ $ RDBGdopp 80 %tutu_var%dopple %tutu_var%dopple %tutu_var%doppgr %tutu_var%doppgr %tutu_var%dopple %tutu_var%dopple * *" UNLESS "RDBGdopp"
  APPEND_COL "spawngrp.2da" "$ $ RDBGspid 80 %tutu_var%spidwr %tutu_var%spidph %tutu_var%spidsw %tutu_scripts%pidphas %tutu_var%spidwr %tutu_var%spidph %tutu_var%spidsw %tutu_scripts%pidphas" UNLESS "RDBGspid"
  APPEND_COL "spawngrp.2da" "$ $ RDBGdead 80 %tutu_var%ghastd %tutu_var%ghastd %tutu_scriptg%houllor %tutu_scriptg%houllor %tutu_scriptbg%skelwa %tutu_scriptbg%skelwa * *"
  APPEND_COL "spawngrp.2da" "$ $ RDBGjell 80 %tutu_var%jellgr %tutu_var%jellgr %tutu_var%jelloc %tutu_var%jelloc %tutu_var%jellmu %tutu_var%jellmu * *" UNLESS "RDBGjell"
  APPEND_COL "spawngrp.2da" "$ $ RDBGcraw 80 %tutu_scriptc%ryptcra %tutu_scriptc%ryptcra %tutu_scriptc%ryptcra %tutu_scriptc%ryptcra %tutu_scriptc%ryptcra * * *" UNLESS "RDBGcraw"
  APPEND_COL "spawngrp.2da" "$ $ RDBGdoom 80 %tutu_var%doomgu %tutu_var%doomgu %tutu_var%doomgu %tutu_var%helmho %tutu_var%helmho %tutu_var%battho * *" UNLESS "RDBGdoom"

  // Make sure table is properly formatted
  COPY_EXISTING "spawngrp.2da" "override"
    PRETTY_PRINT_2DA
END


// ======================
// Outside area (phase 0)
// ======================

COPY_EXISTING "%DurlagsTower%.are" "override"
  // Move unreachable Greater Ghoul
  LAUNCH_PATCH_FUNCTION ALTER_AREA_ACTOR
    INT_VAR
    x_coord		= 410
    y_coord		= 2110
    dest_x		= 410
    dest_y		= 2110
    orient		= 9
    STR_VAR
    actor_name		= "Greater Ghoul"
  END

  // Additional monsters
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    bgt_compat		= 1
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0500.2da"
  END

  // Alter spawn points
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0074) 3

    PATCH_IF FILE_EXISTS_IN_GAME "spawngrp.2da"
    BEGIN
      WRITE_ASCII (spawn_offset + 0x0024) "RDBGdopp" #8
      WRITE_ASCII (spawn_offset + 0x002c) "RDBGdopp" #8
      WRITE_ASCII (spawn_offset + 0x0034) "%tutu_scriptbg%basill" #8
    END
    ELSE
    BEGIN
      WRITE_ASCII (spawn_offset + 0x0024) "dopple" #8
      WRITE_ASCII (spawn_offset + 0x002c) "doppgr" #8
      WRITE_ASCII (spawn_offset + 0x0034) "%tutu_scriptbg%basill" #8
    END
  END


// ===============================
// Cellar & Upper Floors (phase 1)
// ===============================

COPY_EXISTING "%DurlagsTower_Cellar%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0501.2da"
  END


COPY_EXISTING "%DurlagsTower_L2%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0503.2da"
  END


COPY_EXISTING "%DurlagsTower_L4%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0505.2da"
  END


// ====================================
// First three dungeon floors (phase 2)
// ====================================


// Area edits D1

COPY_EXISTING "%DurlagsTower_D1%.are" "override"
  // Additional monsters
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    bgt_compat		= 1
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0511.2da"
  END

  // Alter spawn points
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    PATCH_IF FILE_EXISTS_IN_GAME "spawngrp.2da"
    BEGIN
      WRITE_SHORT (spawn_offset + 0x0074) 4

      WRITE_ASCII (spawn_offset + 0x0024) "RDBGdopp" #8
      WRITE_ASCII (spawn_offset + 0x002c) "RDBGspid" #8
      WRITE_ASCII (spawn_offset + 0x0034) "RDBGdead" #8
      WRITE_ASCII (spawn_offset + 0x003c) "RDBGjell" #8
    END
    ELSE
    BEGIN
      WRITE_SHORT (spawn_offset + 0x0074) 8

      WRITE_ASCII (spawn_offset + 0x0024) "dopple" #8
      WRITE_ASCII (spawn_offset + 0x002c) "doppgr" #8
      WRITE_ASCII (spawn_offset + 0x0034) "spidwr" #8
      WRITE_ASCII (spawn_offset + 0x003c) "spidphas" #8
      WRITE_ASCII (spawn_offset + 0x0024) "ghastd" #8
      WRITE_ASCII (spawn_offset + 0x002c) "ghoullor" #8
      WRITE_ASCII (spawn_offset + 0x0024) "jelloc" #8
      WRITE_ASCII (spawn_offset + 0x002c) "jellmu" #8
    END
  END


// Area edits D2

COPY_EXISTING "%DurlagsTower_D2%.are" "override"
  // Additional monsters
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    bgt_compat		= 1
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0512.2da"
  END


// Sparring dummies on D2 are not hostile (though still red)

COPY_EXISTING "%tutu_var%dumfue.cre" "override"
              "%tutu_var%dumisl.cre" "override"
              "%tutu_var%dumkie.cre" "override"
  PATCH_IF (IDS_OF_SYMBOL("ea" "goodbutred") >= 0)
  BEGIN
    WRITE_BYTE	0x0270 IDS_OF_SYMBOL("ea" "goodbutred")
  END
  ELSE
  BEGIN
    WRITE_BYTE	0x0270 IDS_OF_SYMBOL("ea" "neutral")
  END

  WRITE_ASCII DIALOG "mh#spdum" #8


LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/bg1/durlags_tower/sparring_dummy.d"
END


// More varied undead from crypts in D2 southwest corner

LAUNCH_ACTION_FUNCTION compile_dialog_or_script
  STR_VAR
  path_to_file		= "%MOD_FOLDER%/bg1/durlags_tower/trpghast.baf"
END


// Area edits D3

COPY_EXISTING "%DurlagsTower_D3%.are" "override"
  // Additional monsters
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    bgt_compat		= 1
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0513.2da"
  END


// ==============================
// Elemental Challenges (phase 3)
// ==============================

// Add frost giant to Ice Chamber

COPY "%MOD_FOLDER%/bg1/durlags_tower/mh#frost.itm" "override"
  SAY NAME1 "Attack"
  SAY NAME2 "Attack"


COPY "%MOD_FOLDER%/bg1/durlags_tower/mh#frost.cre" "override"
  SAY NAME1 "Frost Giant"
  SAY NAME2 "Frost Giant"

  PATCH_IF (IDS_OF_SYMBOL("animate" "fire_giant") >= 0)
  BEGIN
    WRITE_LONG 0x0028 IDS_OF_SYMBOL("animate" "fire_giant")
  END

  PATCH_IF (IDS_OF_SYMBOL("animate" "frost_giant") >= 0)
  BEGIN
    WRITE_LONG 0x0028 IDS_OF_SYMBOL("animate" "frost_giant")
  END


COPY_EXISTING "%DurlagsTower_IceChamber%.are" "override"
  LAUNCH_PATCH_MACRO remove_all_area_actors

  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0507.2da"
  END


// Replace annoying phoenix guards in Fire Chamber

ACTION_IF FILE_EXISTS_IN_GAME "elfir01.cre"
BEGIN
  COPY_EXISTING "elfir01.cre" "override/mh#flmgd.cre"
    SAY NAME1 "Flame Guardian"
    SAY NAME2 "Flame Guardian"

    WRITE_ASCII DEATHVAR "PHOENIX" #32


  COPY_EXISTING "elefel01.cre" "override/mh#flame.cre"
END
ELSE
BEGIN
  COPY_EXISTING "mh#blaze.cre" "override/mh#flmgd.cre"
    SAY NAME1 "Flame Guardian"
    SAY NAME2 "Flame Guardian"

    WRITE_ASCII DEATHVAR "PHOENIX" #32


  COPY_EXISTING "mh#blaze.cre" "override/mh#flame.cre"
END


COPY_EXISTING "%DurlagsTower_FireChamber%.are" "override"
  LAUNCH_PATCH_MACRO remove_all_area_actors

  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0508.2da"
  END


// More invisible stalkers in Air Chamber, and move them around a bit

COPY_EXISTING "%DurlagsTower_AirChamber%.are" "override"
  LAUNCH_PATCH_MACRO remove_all_area_actors

  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    bgt_compat		= 1
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0509.2da"
  END


// Give lone fission slime some company in Earth Chamber

COPY_EXISTING "%DurlagsTower_EarthChamber%.are" "override"
  // Additional jellies
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0510.2da"
  END


// =============================
// Final Dungeon Floor (phase 4)
// =============================

// Area edits D4

COPY_EXISTING "%DurlagsTower_D4%.are" "override"
  // Additional monsters
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    INT_VAR
    bgt_compat		= 1
    tutu_compat		= 1
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0514.2da"
  END

  // Alter spawn points
  GET_OFFSET_ARRAY spawn_array ARE_V10_SPAWN_POINTS
  PHP_EACH spawn_array AS int => spawn_offset
  BEGIN
    WRITE_SHORT (spawn_offset + 0x0074) 10
    WRITE_SHORT (spawn_offset + 0x0084) 8

    PATCH_IF FILE_EXISTS_IN_GAME "spawngrp.2da"
    BEGIN
      WRITE_ASCII (spawn_offset + 0x0024) "RDBGdead" #8
      WRITE_ASCII (spawn_offset + 0x002c) "RDBGdead" #8
      WRITE_ASCII (spawn_offset + 0x0034) "RDBGdead" #8
      WRITE_ASCII (spawn_offset + 0x003c) "RDBGdead" #8
      WRITE_ASCII (spawn_offset + 0x0044) "RDBGcraw" #8
      WRITE_ASCII (spawn_offset + 0x004c) "RDBGcraw" #8
      WRITE_ASCII (spawn_offset + 0x0054) "RDBGcraw" #8
      WRITE_ASCII (spawn_offset + 0x005c) "RDBGjell" #8
      WRITE_ASCII (spawn_offset + 0x0064) "RDBGjell" #8
      WRITE_ASCII (spawn_offset + 0x006c) "RDBGdoom" #8
    END
    ELSE
    BEGIN
      WRITE_ASCII (spawn_offset + 0x0024) "ghast" #8
      WRITE_ASCII (spawn_offset + 0x002c) "ghast" #8
      WRITE_ASCII (spawn_offset + 0x0034) "ghoullor" #8
      WRITE_ASCII (spawn_offset + 0x003c) "ghoullor" #8
      WRITE_ASCII (spawn_offset + 0x0044) "cryptcra" #8
      WRITE_ASCII (spawn_offset + 0x004c) "cryptcra" #8
      WRITE_ASCII (spawn_offset + 0x0054) "cryptcra" #8
      WRITE_ASCII (spawn_offset + 0x005c) "jellmu" #8
      WRITE_ASCII (spawn_offset + 0x0064) "jellmu" #8
      WRITE_ASCII (spawn_offset + 0x006c) "helmho" #8
    END
  END


// Demon Knight edits

COPY_EXISTING "%tutu_var%deathk.cre" "override"
  REPLACE_CRE_ITEM "sw2h08" #0 #0 #0 "unstealable" "weapon1" EQUIP TWOHANDED
  ADD_CRE_ITEM "%tutu_var%plat05" #0 #0 #0 "unstealable" "armor"
  ADD_CRE_ITEM "%tutu_var%helm02" #0 #0 #0 "unstealable" "qitem2"
  REMOVE_CRE_ITEM "%tutu_var%shld06"



COPY_EXISTING "%tutu_var%deathk1.cre" "override"
  REPLACE_CRE_ITEM "sw2h08" #0 #0 #0 "unstealable" "weapon1" EQUIP TWOHANDED
  REPLACE_CRE_ITEM "%tutu_scriptbg%helm15" #0 #0 #0 "unstealable" "helmet"
  ADD_CRE_ITEM "%tutu_var%helm02" #0 #0 #0 "unstealable" "qitem2"
  REMOVE_CRE_ITEM "%tutu_var%shld06"

  PATCH_IF FILE_EXISTS_IN_GAME "minhp1.itm"
  BEGIN
    ADD_CRE_ITEM "minhp1" #0 #0 #0 "unstealable&undroppable" "belt"
  END


// Demon Knight gets some backup

COPY_EXISTING "%tutu_scriptbg%skelwa.cre" "override/mh#dkskw.cre"
  WRITE_LONG	0x0014 (THIS + 1000)	// XP value
  WRITE_SHORT	0x0024 (THIS + 16)	// Current HP
  WRITE_SHORT	0x0026 (THIS + 16)	// Maximum HP
  WRITE_SHORT	0x0024 (THIS - 2)	// Natural AC
  WRITE_SHORT	0x0026 (THIS - 2)	// Effective AC
  WRITE_BYTE	0x0052 (THIS - 2)	// THAC0
  WRITE_BYTE	0x0054 (THIS - 1)	// Save vs. death
  WRITE_BYTE	0x0055 (THIS - 1)	// Save vs. wand
  WRITE_BYTE	0x0056 (THIS - 1)	// Save vs. poly
  WRITE_BYTE	0x0057 (THIS - 1)	// Save vs. breath
  WRITE_BYTE	0x0058 (THIS - 1)	// Save vs. spell
  WRITE_BYTE	0x0234 (THIS + 2)	// Level


COPY_EXISTING "%DurlagsTower_DemonknightsChamber%.are" "override"
  LAUNCH_PATCH_FUNCTION add_area_actors_from_2da
    STR_VAR
    path_to_2da		= "%MOD_FOLDER%/bg1/durlags_tower/bg0516.2da"
  END


