// Winter Wolf fixes

ACTION_IF NOT FILE_EXISTS_IN_GAME "mh#wlfwi.itm"
BEGIN
  COPY_EXISTING "p2-8.itm" "override/mh#wlfwi.itm"
    SAY NAME1 "Attack"
    SAY NAME2 "Attack"

    WRITE_ASCII 0x003a "iwolf" #8

    LAUNCH_PATCH_FUNCTION ALTER_ITEM_HEADER
      STR_VAR
      icon		= "iwolf"
    END

    LAUNCH_PATCH_FUNCTION ADD_ITEM_EFFECT
      INT_VAR
      type		= 1	// Melee
      opcode		= 12	// HP: Damage
      target		= 2	// Pre-target
      timing		= 1	// Permanent
      parameter2		= 131072
      probability1	= 40
      dicenumber		= 1
      dicesize		= 6
    END


  ADD_SPELL "%MOD_FOLDER%/creature_corrections/spells/winter_wolf_breath.spl" 3 1 MH_WINTER_WOLF_BREATH
    SAY NAME1 "Winter Wolf Breath"


  LAUNCH_ACTION_FUNCTION compile_dialog_or_script
    STR_VAR
    path_to_file		= "%MOD_FOLDER%/creature_corrections/scripts/mh#wlfwi.baf"
  END
END


DEFINE_PATCH_MACRO fix_winter_wolf
BEGIN
  REPLACE_CRE_ITEM "mh#wlfwi" #0 #0 #0 "unstealable&undroppable" "weapon1" EQUIP
  REPLACE_CRE_ITEM "mh#wlfwi" #0 #0 #0 "unstealable&undroppable" "weapon2"
  REPLACE_CRE_ITEM "mh#wlfwi" #0 #0 #0 "unstealable&undroppable" "weapon3"
  REPLACE_CRE_ITEM "mh#wlfwi" #0 #0 #0 "unstealable&undroppable" "weapon4"

  WRITE_BYTE	0x0052 15	// THAC0

  LAUNCH_PATCH_FUNCTION replace_cre_script
    INT_VAR
    check_default	= 0
    STR_VAR
    old_script		= "wntrwolf"
    new_script		= "mh#wlfwi"
    RET
    found
  END

  PATCH_IF !found
  BEGIN
    WRITE_ASCII SCRIPT_RACE "mh#wlfwi" #8
  END

  WRITE_ASCII SCRIPT_DEFAULT "wtasight" #8
END


