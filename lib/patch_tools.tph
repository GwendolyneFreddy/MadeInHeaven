DEFINE_PATCH_FUNCTION add_energy_drain
INT_VAR
levels_drained			= 1
BEGIN
  READ_ASCII 0x0000 signature (4)
  PATCH_MATCH "%signature%"
  WITH
  "ITM " WHEN ENGINE_IS "bg1 totsc iwd1 how totlm"
  BEGIN
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EFFECT
      INT_VAR
      type			= 1	// Melee
      opcode			= 18	// HP: Maximum HP Modifier
      target			= 2	// Pre-target
      duration			= 2400
      parameter1		= ((0 - 5) * levels_drained)
    END

    LAUNCH_PATCH_FUNCTION ADD_ITEM_EFFECT
      INT_VAR
      type			= 1	// Melee
      opcode			= 54	// Stat: THAC0 Modifier
      target			= 2	// Pre-target
      duration			= 2400
      parameter1		= ((0 - 2) * levels_drained)
    END

    LAUNCH_PATCH_FUNCTION ADD_ITEM_EFFECT
      INT_VAR
      type			= 1	// Melee
      opcode			= 142	// Graphics: Display Special Effect Icon
      target			= 2	// Pre-target
      duration			= 2400
      parameter2		= 53
    END
  END

  "ITM "
  BEGIN
    LAUNCH_PATCH_FUNCTION ADD_ITEM_EFFECT
      INT_VAR
      type			= 1	// Melee
      opcode			= 216	// Spell Effect: Level Drain
      target			= 2	// Pre-target
      timing			= 1	// Permanent
      parameter1		= levels_drained
    END
  END

  "SPL " WHEN ENGINE_IS "bg1 totsc iwd1 how totlm"
  BEGIN
    LAUNCH_PATCH_FUNCTION ADD_SPELL_EFFECT
      INT_VAR
      opcode			= 18	// HP: Maximum HP Modifier
      target			= 2	// Pre-target
      duration			= 2400
      parameter1		= ((0 - 5) * levels_drained)
    END

    LAUNCH_PATCH_FUNCTION ADD_SPELL_EFFECT
      INT_VAR
      opcode			= 54	// Stat: THAC0 Modifier
      target			= 2	// Pre-target
      duration			= 2400
      parameter1		= ((0 - 2) * levels_drained)
    END

    LAUNCH_PATCH_FUNCTION ADD_SPELL_EFFECT
      INT_VAR
      opcode			= 142	// Graphics: Display Special Effect Icon
      target			= 2	// Pre-target
      duration			= 2400
      parameter2		= 53
    END
  END

  "SPL "
  BEGIN
    LAUNCH_PATCH_FUNCTION ADD_SPELL_EFFECT
      INT_VAR
      type			= 1	// Melee
      opcode			= 216	// Spell Effect: Level Drain
      target			= 2	// Pre-target
      timing			= 1	// Permanent
      parameter1		= levels_drained
    END
  END
  DEFAULT
    PATCH_FAIL "add_energy_drain() used on incompatible file!"
  END
END


DEFINE_PATCH_FUNCTION add_magic_missile_immunity
  INT_VAR
  match_opcode		= 142	// Graphics: Display Special Effect Icon
BEGIN
  PATCH_FOR_EACH projectile IN 36 67 68 69 70 71 72 73 74 75 76 77
  BEGIN
    LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
      match_opcode
      multi_match	= 1	// Just once, please
      silent		= 1	// Don't care about warnings
      opcode		= 83	// Protection: From Projectile
      parameter1	= 0	// Irrelevant
      parameter2	= projectile
      STR_VAR
      insert		= "last"
    END
  END

  PATCH_IF NOT ENGINE_IS "bg1 totsc"
  BEGIN
    PHP_EACH magic_missiles AS spell_name => spell_res
    BEGIN
      LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
        match_opcode
        multi_match	= 1	// Just once, please
        silent		= 1	// Don't care about warnings
        opcode		= 206	// Spell: Protection From Spell
	parameter1	= 0
	parameter2	= 0
        STR_VAR
        insert		= "last"
        resource	= "%spell_res%"
      END
    END
  END
END


DEFINE_PATCH_FUNCTION prevent_spell_effect_stacking
  INT_VAR
  match_opcode		= 142
BEGIN
  PATCH_IF ENGINE_IS "iwd1 how totlm"
  BEGIN
    LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
      match_opcode
      multi_match	= 1
      silent		= 1	// Don't care about warnings
      opcode		= 254	// Remove effects by resource
      timing		= 0	// Duration
      duration		= 0
      parameter1	= 0
      parameter2	= 0
      STR_VAR
      insert		= "first"
      resource		= "%SOURCE_RES%"
    END
  END

  PATCH_IF ENGINE_IS "bgee bg2ee"
  BEGIN
    LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
      match_opcode
      multi_match	= 1
      silent		= 1	// Don't care about warnings
      opcode		= 321	// Remove effects by resource
      timing		= 0	// Duration
      duration		= 0
      parameter1	= 0
      parameter2	= 0
      STR_VAR
      insert		= "first"
      resource		= "%SOURCE_RES%"
    END
  END

  PATCH_IF ENGINE_IS "bg2 tob"
  BEGIN
    LAUNCH_PATCH_FUNCTION CLONE_EFFECT
      INT_VAR
      match_opcode
      multi_match	= 1
      silent		= 1	// Don't care about warnings
      opcode		= 206	// Spell Effect: Immunity Spell
      parameter1	= RESOLVE_STR_REF ("Multiple castings of this spell have no effect.")
      parameter2	= 0	// Default (IWD1)
      STR_VAR
      insert		= "last"
      resource		= "%SOURCE_RES%"
    END
  END
END


DEFINE_PATCH_FUNCTION replace_cre_script
  INT_VAR
  check_override	= 1
  check_class		= 1
  check_race		= 1
  check_general		= 1
  check_default		= 1
  STR_VAR
  old_script		= ""
  new_script		= ""
  RET
  found
BEGIN
  SET found = 0

  PATCH_IF check_override
  BEGIN
    READ_ASCII SCRIPT_OVERRIDE script

    PATCH_IF ("%script%" STRING_EQUAL_CASE "%old_script%")
    BEGIN
      WRITE_EVALUATED_ASCII SCRIPT_OVERRIDE "%new_script%"
      SET found = 1
    END
  END

  PATCH_IF check_class
  BEGIN
    READ_ASCII SCRIPT_CLASS script

    PATCH_IF ("%script%" STRING_EQUAL_CASE "%old_script%")
    BEGIN
      WRITE_EVALUATED_ASCII SCRIPT_CLASS "%new_script%"
      SET found = 1
    END
  END

  PATCH_IF check_race
  BEGIN
    READ_ASCII SCRIPT_RACE script

    PATCH_IF ("%script%" STRING_EQUAL_CASE "%old_script%")
    BEGIN
      WRITE_EVALUATED_ASCII SCRIPT_RACE "%new_script%"
      SET found = 1
    END
  END

  PATCH_IF check_general
  BEGIN
    READ_ASCII SCRIPT_GENERAL script

    PATCH_IF ("%script%" STRING_EQUAL_CASE "%old_script%")
    BEGIN
      WRITE_EVALUATED_ASCII SCRIPT_GENERAL "%new_script%"
      SET found = 1
    END
  END

  PATCH_IF check_default
  BEGIN
    READ_ASCII SCRIPT_DEFAULT script

    PATCH_IF ("%script%" STRING_EQUAL_CASE "%old_script%")
    BEGIN
      WRITE_EVALUATED_ASCII SCRIPT_DEFAULT "%new_script%"
      SET found = 1
    END
  END
END


DEFINE_PATCH_FUNCTION make_magical
  INT_VAR
  enchantment	= "-1"
  lore		= "-1"
BEGIN
  WRITE_LONG	0x0018 (THIS | BIT6)

  PATCH_IF enchantment >= 0
  BEGIN
    WRITE_LONG	0x0060 enchantment
  END

  PATCH_IF lore >= 0
  BEGIN
    WRITE_SHORT	0x0042 lore
  END
END


