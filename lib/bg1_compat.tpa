// BG1 compatibility hacks

ACTION_IF NOT FILE_EXISTS "override/bg1_compat_hacks.mih"
BEGIN
  COPY "%MOD_FOLDER%/dummy.mih" "override/bg1_compat_hacks.mih"

  // Create missing effect files

  OUTER_FOR (i = 1; i <= 3; ++i)
  BEGIN
    CREATE EFF "undhit%i%"
      WRITE_LONG	0x0010 178	// THAC0 vs. Creature Type
      WRITE_LONG	0x001c IDS_OF_SYMBOL("general" "undead")
      WRITE_LONG	0x0020 3
      WRITE_SHORT	0x0024 1	// Permanent
      WRITE_SHORT	0x002c 100	// 100% chance
      WRITE_LONG	0x0060 i

    CREATE EFF "unddam%i%"
      WRITE_LONG	0x0010 179	// Damage vs. Creature Type
      WRITE_LONG	0x001c IDS_OF_SYMBOL("general" "undead")
      WRITE_LONG	0x0020 3
      WRITE_SHORT	0x0024 1	// Permanent
      WRITE_SHORT	0x002c 100	// 100% chance
      WRITE_LONG	0x0060 i
  END


  // Create missing attack items

  COPY_EXISTING "p1-4.itm" "override/p2-8.itm"
    LAUNCH_PATCH_FUNCTION ALTER_ITEM_HEADER
      INT_VAR
      dicenumber	= 2
    END


  // Restore missing Buckler +1 (shld17)

  COPY_LARGE "%MOD_FOLDER%/lib/bg1_compat/ishld17.bam" "override"

  COPY "%MOD_FOLDER%/lib/bg1_compat/shld17.itm" "override"
    SAY NAME2 "Buckler +1"
    SAY IDENTIFIED_DESC "A buckler is a small shield of wood or metal that fastens to the forearm.  Because of its size it is light and can be worn with very little restriction to movement.  This buckler has some form of generic enchantment, making it more resistant to blows.

STATISTICS:

Armor Class Bonus:  2
Special:  No Missile/Piercing Attack Protection
Weight:  2
Requires: 4 Strength
Unusable By:
 Mage"


  // Copy in extended state.bams (from ToB, uncompressed)

  COPY "%MOD_FOLDER%/lib/bg1_compat/states.bam" "override"
END


