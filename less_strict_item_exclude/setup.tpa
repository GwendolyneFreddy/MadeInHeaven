// Wear Protection Items with Magical_Armor
//
// This mod patches itemexcl.2da so that all magical armor is removed
// from it.  This allows combining, say, a Ring of Protection +1 with
// a Chain Mail +1, but not a Ring of Protection +1 with a Cloak of
// Protection +1.
//
// This makes protection items actually useful for characters other
// than mages, without allowing characters to just pile it on.


PRINT "Patching itemexcl.2da..."

COPY_EXISTING "itemexcl.2da" "override"
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows

  READ_2DA_ENTRIES_NOW __read_itemexcl cols

  FOR (i = 0; i < rows; ++i)
  BEGIN
    READ_2DA_ENTRY_FORMER __read_itemexcl i 0 item

    PATCH_IF FILE_EXISTS_IN_GAME "%item%.itm"
    BEGIN
      INNER_PATCH_FILE "%item%.itm"
      BEGIN
        READ_SHORT 0x001c item_type
      END
      
      PATCH_IF (item_type == 2)
      BEGIN
        SET_2DA_ENTRY_LATER __set_itemexcl i 1 0
      END
    END
  END

  SET_2DA_ENTRIES_NOW __set_itemexcl cols

  BUT_ONLY_IF_IT_CHANGES


